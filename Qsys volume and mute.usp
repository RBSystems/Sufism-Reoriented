/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name: BBI Engineering, Inc.
System Name: Qualcomm N Auditorium
System Number:
Programmer: Sam Wong
Comments:
*/

/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
// #ENABLE_DYNAMIC
#SYMBOL_NAME "Q-Sys Volume and Mute"
#HINT "Control Volumes and Mutes"

#DEFINE_CONSTANT MAX_STR_LEN	 		256
#DEFINE_CONSTANT MAX_NAME_LEN			20
#DEFINE_CONSTANT MAX_PARAM_LEN			30
#DEFINE_CONSTANT N_PARAMETERS			30	

// #DEFINE_CONSTANT
// #CATEGORY "" 
// #PRINT_TO_TRACE
// #DIGITAL_EXPAND 
// #ANALOG_SERIAL_EXPAND 
// #OUTPUT_SHIFT 
// #HELP_PDF_FILE ""
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE
// #ENCODING_ASCII
// #ENCODING_UTF16
// #ENCODING_INHERIT_FROM_PARENT
// #ENCODING_INHERIT_FROM_PROGRAM
/*
#HELP_BEGIN
   (add additional lines of help lines)
#HELP_END
*/

/*******************************************************************************************
  Include Libraries
  (Uncomment and include additional libraries as needed)
*******************************************************************************************/
// #CRESTRON_LIBRARY ""
// #USER_LIBRARY ""

/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT connected;
DIGITAL_INPUT poll;
DIGITAL_INPUT invalidate;
DIGITAL_INPUT Mute_Toggle[N_PARAMETERS];
DIGITAL_INPUT Mute_On[N_PARAMETERS];
DIGITAL_INPUT Mute_Off[N_PARAMETERS];
STRING_INPUT _SKIP_;
STRING_INPUT rx$[512];
ANALOG_INPUT _SKIP_;
ANALOG_INPUT Volume[N_PARAMETERS];
STRING_INPUT _SKIP_;
STRING_INPUT ControlID[N_PARAMETERS][MAX_NAME_LEN];

DIGITAL_OUTPUT _SKIP_, _SKIP_, _SKIP_, _SKIP_;
DIGITAL_OUTPUT Mute_FB[N_PARAMETERS];
STRING_OUTPUT _SKIP_;
STRING_OUTPUT tx$;
ANALOG_OUTPUT _SKIP_;
ANALOG_OUTPUT Volume_FB[N_PARAMETERS];

/*******************************************************************************************
  Parameters
  (Uncomment and declare parameters as needed)
*******************************************************************************************/
INTEGER_PARAMETER ChangeGroup_ID;

/*******************************************************************************************
  Parameter Properties
  (Uncomment and declare parameter properties as needed)
*******************************************************************************************/
/*
#BEGIN_PARAMETER_PROPERTIES parameter_variable, parameter_variable, ...
   // propValidUnits = // unitString or unitDecimal|unitHex|unitPercent|unitCharacter|unitTime|unitTicks;
   // propDefaultUnit = // unitString, unitDecimal, unitHex, unitPercent, unitCharacter, unitTime or unitTicks;
   // propBounds = lower_bound , upper_bound;
   // propDefaultValue = ;  // or, propDefaultValue = "";
   // propList = // { "value" , "label" } , { "value" , "label" } , ... ;
   // propShortDescription = "status_bar_hint_text";
   // #BEGIN_PROP_FULL_DESCRIPTION  line_1...  line_2...  line_n  #END_PROP_FULL_DESCRIPTION
   // #BEGIN_PROP_NOTES line_1...  line_2...  line_n  #END_PROP_NOTES
#END_PARAMETER_PROPERTIES
*/

/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: myString = "";
*******************************************************************************************/
String muteNames[N_PARAMETERS][MAX_PARAM_LEN];
String volumeNames[N_PARAMETERS][MAX_PARAM_LEN];
Integer nParams;
// LONG_INTEGER
// SIGNED_INTEGER
// SIGNED_LONG_INTEGER
// STRING

/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/

Function SendToQsys(STRING sMsgToSend)
{
	tx$ = sMsgToSend;
	ProcessLogic();
}

Function PollChangeGroup()
{    
	STRING sToSend[32];

	if(connected)
	{
		sToSend = "cgp " + ITOA(ChangeGroup_ID) + "\n";
		SendToQsys(sToSend);
	}
}

Function InitChangeGroup()
{
	STRING toSend$[256];
	Integer i;

	toSend$ = "cgc " + ITOA(ChangeGroup_ID) + "\n";
	SendToQsys(toSend$);
    for (i = 1 to nParams)
	{
		toSend$ = "cga " + ITOA(ChangeGroup_ID) + " " + muteNames[i] + "\n";
		SendToQsys(toSend$);
		toSend$ = "cga " + ITOA(ChangeGroup_ID) + " " + volumeNames[i] + "\n";
		SendToQsys(toSend$);
	}
}

String_Function itop(Integer analogInput)
{
	//assumes analog Input is scaled from 0 to 65535
	String toReturn$[256];
	Long_Integer dividend;
	Long_Integer divisor;
	Long_Integer quotient;
	Long_Integer remainder;
	Long_Integer x;
	
	toReturn$ = "";
	dividend = analogInput;
	divisor = 65535;
	
	for(x=1 to 4)
	{
		quotient = dividend / divisor;
		remainder = dividend % divisor;
		toReturn$ = toReturn$ + LTOA(quotient);
		
		if (x = 1)
			toReturn$ = toReturn$ + ".";
		
		//set new dividend
		dividend = remainder * 10;
	}
	
	Return (toReturn$);
}

Integer_Function ptoi(String positionString$)
{
	Integer toReturn;
	Integer maxAnalog;
	Integer val;
	Integer nDigits;
	String junk$[16];
	String currentDigit$[16];
	
	maxAnalog = 65535;
	
	if (positionString$ = "1\r\n" || positionString$ = "1 ")
		toReturn = maxAnalog;
	else if (positionString$ = "0\r\n" || positionString$ = "0 ")
		toReturn = 0;
	else
	{
		//start after 0.
		if (len(positionString$) < 3)
			Return (0);
		junk$ = REMOVEBYLENGTH(2,positionString$);
		nDigits = len(positionString$) - 2;
		if (nDigits > 4)
		{
			positionString$ = Left(positionString$, 4);
			nDigits = 4;
		}
		val = ATOI(positionString$);
		switch (nDigits)
		{
			case (1):
			{
				toReturn = val * 6553 + val / 2;
			}
			case (2):
			{
				toReturn = val * 655 + val * 7 / 20;
			}
			case (3):
			{
				toReturn = val * 65 + val / 2 + val * 7 / 200;
			}
			case (4):
			{
				toReturn = val * 6 + val / 2 + val / 20 + val * 7 / 2000;
			}
			default:
			{
				toReturn = 0;
			}
		}
	}
	Return (toReturn);
}

Function ParseMute(String incoming$)
{
	Integer i;
    
	if (!find("cv", incoming$))
		return;
	for (i = 1 to nParams)
	{
		if (!find(muteNames[i], incoming$))
			continue;
		// found it
		if (find("unmuted", incoming$))
			Mute_FB[i] = 0;
		else
			Mute_FB[i] = 1;
		break;
	}
}

Function ParseVolume(String incoming$)
{
	Integer i;
	Integer pos;

	if (!find("cv", incoming$))
		return;
	for (i = 1 to nParams)
	{
		if (!find(volumeNames[i], incoming$))
			continue;
		// found it
		pos = ReverseFindNoCase(" ", incoming$);
		if (pos = 0)
			break;	
		pos = len(incoming$) - pos;
		incoming$ = right(incoming$, pos);
		Volume_FB[i] = ptoi(incoming$);
		break;
	}
}
     
Function DoMute(Integer which, Integer val)
{
	STRING sToSend[256];

	//send Qsys command
	MakeString(sToSend, "csv %s %d\n", muteNames[which], val);
	SendToQsys(sToSend);
	CancelAllWait();
	Wait (10, DelayedPollMute) PollChangeGroup();
}

/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/

PUSH Mute_Toggle
{
	INTEGER lastModifiedIndex;
	INTEGER val;
	
	lastModifiedIndex = GetLastModifiedArrayIndex();

	val = !Mute_FB[lastModifiedIndex];	
	DoMute(lastModifiedIndex, val);	
}

PUSH Mute_On
{
	INTEGER lastModifiedIndex;
	
	lastModifiedIndex = GetLastModifiedArrayIndex();

	DoMute(lastModifiedIndex, 1);	
}

PUSH Mute_Off
{
	INTEGER lastModifiedIndex;
	
	lastModifiedIndex = GetLastModifiedArrayIndex();

	DoMute(lastModifiedIndex, 0);	
}

CHANGE Volume
{
	INTEGER lastModifiedIndex;
	STRING sToSend[256];
	STRING cCommand[256];
	
	lastModifiedIndex = GetLastModifiedArrayIndex();
	
	if(Volume[lastModifiedIndex] = Volume_FB[lastModifiedIndex])
	{
		//do nothing
	}
	else
	{				
		//send Qsys command
		sToSend = "csp " + volumeNames[lastModifiedIndex] + " " + itop(Volume[lastModifiedIndex]) + "\n";
		SendToQsys(sToSend);
	}
	CancelAllWait();
	Wait (10, DelayedPollVolume) PollChangeGroup();
}

threadsafe CHANGE rx$
{
	String incoming$[MAX_STR_LEN];

	incoming$ = rx$;
	ParseMute(incoming$);
	ParseVolume(incoming$);
}
 
PUSH Connected
{
	InitChangeGroup();
}

PUSH poll
{
	PollChangeGroup();
}

PUSH invalidate
{
	STRING sToSend[32];
	
	if(connected)
	{
		sToSend = "cgi " + ITOA(ChangeGroup_ID) + "\n";
		SendToQsys(sToSend);
	}
}

/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/
Function Main()
{
	integer i;

    // TODO:  Add code here
    // Initialize declared global and local variables/arrays as needed.
    WaitForInitializationComplete();
                
	// let's build the parameter names
	nParams = 0;
	for (i = 1 to N_PARAMETERS)
	{
		if (len(ControlID[i]) > 0)
		{
			muteNames[i] = ControlID[i] + "MUTE";
			volumeNames[i] = ControlID[i] + "VOL";
			nParams = nParams + 1;
		}
		else
			break;
	}
}
